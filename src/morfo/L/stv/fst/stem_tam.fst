## perfective, imperfective, gerundive, jussive-imperative stems
-> start

#### MULTIPLE LANGUAGES AND TAMs

### PREFIXES

start -> a          [e:]      [vc=tr]
# Perfective and gerundive (Ti,Am); imperative (all languages)
# te- can become t- in Silt'e
start -> te         <*t:>     [vc=ps]
start -> at        <et:>     [vc=cs]
# For cases where Ti imperfective passive has no prefix (CCC, CC_C, CCCC)

te -> simp          [:]
a -> simp           [:]       [as=smp];[as=it]
start -> simp       [:]       [vc=smp]

### END

-1 -> end           [X/L-y,Y]

# Final vowels for most situations
-2V -> -1           [e:]      [tm=imf,vc=ps];[tm=j_i,vc=ps];[tm=prf]
-2V -> -1           [:]       [tm=imf,vc=smp];[tm=imf,vc=cs];[tm=imf,vc=tr];[tm=j_i,vc=cs];[tm=j_i,vc=tr];[tm=j_i,vc=smp]

# Final ', Y, y
-2V -> end          [a:']     [tm=prf];[tm=imf];[tm=j_i]
# Keep the Y because we need it for palatalization of the previous consonant
-2V -> -2Va         [Y;:y]    [tm=prf]
-2Va -> end         [E:]
-2V -> end          [Y;:y]    [tm=imf];[tm=j_i]

### A (CCC)

-2A_ -> -2V         [:]       [tm=imf];[tm=prf];[tm=j_i,vc=tr];[tm=j_i,vc=cs]
# e for ps and smp in jussive-imperative
-2A_ -> -1          [e:]      [tm=j_i,vc=ps];[tm=j_i,vc=smp]

-2A -> -2A_         [X/L]     [tm=prf];[tm=j_i]
-2A -> -2A_         [X/L]     [tm=imf]
-2An -> -2A         [n:M]
-2An -> -2A         [:]
-3AV -> -2An        [e:]      [tm=prf]
-3AV -> -2An        [e:]      [tm=imf];[tm=j_i,vc=ps];[tm=j_i,vc=cs]
# First vowel in CCC jussive/imperative non-passive
-3AV -> -2An        [:]       [tm=j_i,vc=smp];[tm=j_i,vc=tr]
-3 -> -3AV          [X]

# initial '; no vc=tr possible
-3AL -> -2A         [a:']     [tm=prf,vc=smp];[tm=prf,vc=ps];[tm=prf,vc=cs];[tm=ger,vc=smp];[tm=ger,vc=ps];[tm=ger,vc=cs]
-3AL -> -2A         [a:']     [tm=imf,vc=smp];[tm=imf,vc=ps];[tm=imf,vc=cs];[tm=j_i,vc=ps];[tm=j_i,vc=cs]
-3AL -> -2A         [I:']     [tm=j_i,vc=smp]

simp -> -3          [:]
simp -> -3AL         [:]

### A (CLC, imperfective)

#-2LV -> -1          [a:]      [vc=ps]
#-2LV -> -1          [I:]      [vc=smp];[vc=tr];[vc=cs]
#-2L -> -2LV         [L]
#-3LV -> -2L         [e:]      [vc=ps]
#-3LV -> -2L         [:]       [vc=smp];[vc=tr];[vc=cs]

-3LV -> -1          [a:']     [tm=prf];[tm=imf,vc=ps];[tm=j_i,vc=ps];[tm=j_i,vc=smp]
-3LV -> -1          [i:']     [tm=imf,vc=smp];[tm=imf,vc=tr];[tm=imf,vc=cs];[tm=j_i,vc=tr];[tm=j_i,vc=cs]

-3L -> -3LV         [X/L]

simp -> -3L         [:]
# a -> -3L            [:]       [tm=imf]
# start -> -3L        [t:]      [tm=imf]
# start -> -3L        [:]       [tm=imf,vc=smp]

### B (CC_C)

-2B_ -> -2V         [_]

-2B -> -2B_         [X]
-3BV -> -2B         [e:]
-3B -> -3BV         [X]

# initial L; no vc=tr
-3AL -> -2B         [a:']     [vc=smp];[vc=ps];[vc=cs]

simp -> -3B         [:]

### Quad (CCCC)

# geminated
-2.4 -> -2.4._      [:]
# not geminated
-2.4 -> -2.4*       [:]
-2.4._ -> -2.4_     [X/L]
-2.4_ -> -2V        [_]
# not geminated
-2.4* -> -2V        [X/L]
-2.4* -> -2V        [:']
# long vowels probably only possible with C1='
-3.4V -> -2.4._     [e:]                     # ;a;O;E]
-3.4V -> -2.4*      [e:]   [tm=prf];[tm=imf] # ;a;O;E]
# only if not geminated
-3.4V -> -2.4*      [:]    [tm=j_i]          # ;a;O;E] 

-3.4 -> -3.4V       [X]
-4.4V -> -3.4       [e:]

-4.4 -> -4.4V       [X/L]

# First consonant of CCCC verbs
simp -> -3.4        [X/L]
# Lexical n possible after C1='
simp -> -3.4n       [I:']
-3.4n -> -3.4       [n:M;:]

### ...aCC: C (CaCC), CCaCC

-3an -> -2.4        [n:M;:]
-3aV -> -3an        [O;E;U;i;a]
# C'C reduplication
-3aV ->  -1         [a:']          [as=it]

-3a -> -3aV         [X/L]
-4aV -> -3a         [a;E;i]           # E,i needed for etmIsEsakkere, etmIsisakkIr

# C1=' reduplication
-4aVL -> -4anL      [I:a;I:E;u:O]
-4anL -> -3aL       [n:M;:]
-3aL -> -3aVL       [X/L]
-3aVL -> -2.4       [a]

-4a -> -4aV         [X/L]

simp -> -3a         [:;X/L]
simp -> -4a         [:;X/L]
simp -> -4aVL       [:']          [as=it]

at -> -3a           [:;X/L]
at -> -4a           [:;X/L]
# Drop initial ' after at
at -> -3aV          [:']
at -> -4aVL         [:']          [as=it]

### ...'CVCCC

-3a.n -> -3a.        [n:M;:]
-3a.V -> -2.4        [O;E;U;i;a]

-3a. -> -3a.V        [X]
-4a.V -> -3a.        [a;E;i]          # E,i needed for etmIsEsakkere, etmIsisakkIr

-4a. -> -4a.V        [X]

-4a.n -> -4a.        [n:M;:]

simp -> -4a.n        [I:']
simp -> -3a.n        [I:']

at -> -4a.n          [I:']
at -> -3a.n          [I:']

end ->
